
/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

/*1.AP INDEX*/
AP_INDEX = FRONT_CODEC;

/*2.从地址map图中获取card模块的常驻代码段物理地址，只需写低14位，开发人员需填写，集成开始后只由集成人员修改，驱动人员不允许修改*/
SRAM_TEXT_ADDR = SRAM_UI_RCODE_ADDR;
SRAM_TEXT_LEN  = SRAM_UI_RCODE_SIZE;
SRAM_DATA_ADDR = SRAM_UI_DATA_ADDR;
SRAM_DATA_LEN = SRAM_UI_DATA_SIZE;

/*3.转换为链接地址,不能修改*/
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;
SROM_TEXT_ADDR = RCODE_ADDR_BASE + SROM_STUB_ADDR_MDY;


/*只是建议起始地址，也可以往后偏移，如control_2一般不是从这个地址，如果要占全部的空间，则使用control_1*/
BANK_BASAL_ADDR_BASE = (AP_BANK_FRONT_BASAL << 24) + SRAM_AP_BANK_BACK_CONTROL_ADDR;/*图片中间件修改AP_BANK_BACK_BASAL为AP_BANK_FRONT_BASAL/

/*bank的实际空间大小,不能修改*/
BANK_BASAL_SIZE = SRAM_AP_BANK_BACK_BASAL_SIZE;

/*bank的实际空间大小,不能修改*/
SROM_TEXT_SIZE = SROM_STUB_SIZE_MDY;

/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)

ENTRY(stub_op_entry)

EXTERN(sys_op_entry)
SECTIONS
{
  
	/* 固化代码段: .rom */
	.rom_text SROM_TEXT_ADDR : /*0xBFC03b00~0xBFC03FFF*/
	{
	    ud_rom.o(.text)	      
		. = SROM_TEXT_SIZE;
	}
	  
	/* 常驻代码段: .text */
    .text RCODE_TEXT_ADDR	: 
    {      
        /*API OBJ在此链接*/
        sys_op_entry.o(.text)
        
        ud_stub_oper.o(.text .rodata)
        . = SRAM_TEXT_LEN;      
    }
    
    .data RDATA_DATA_ADDR   :
    {
        ud_stub_entry.o(.text .rodata .data)
        ud_data.o(.data)
        ud_data.o(.bss)  
        /*这里数据可能可以优化*/        
        ud_setup.o(.data .bss)
        . = SRAM_DATA_LEN;                      
    }
    
    /*6.链接bank control 1代码*/
    . = BANK_BASAL_ADDR_BASE;
    /*OFFSET 为BACK_BANK 1组的实际物理地址*/
    OFFSET = . & 0x3ffff;
    BANK_BASAL_0   :
    {    	  
    	ud_deinit.o(.text .rodata .xdata)
    	ud_stub_exit.o(.text .rodata .xdata) 
        ud_init.o(.text .rodata)  
    }
    
    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;  
    BANK_BASAL_1   :
    {       
		ud_stub_init.o(.text .rodata .xdata)
        ud_setup.o(.text .rodata .data)
        ud_set_blkep.o(.text .rodata .xdata)
    }
}
